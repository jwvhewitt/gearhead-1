Plot 0
	name <PLOT: Hired Hunter>
	% Edited to v0.901 standard

	% E1 is the scene for the current episode.
	% E2 is the character who will tell the PC where to go.
	% E3 is the scene to be used in the next episode.
	% E4 is the hired hunter himself.
	% E5 is the faction that did the hiring.
	Element1 <Grab 3>
	Element2 <Character HasMecha Heroic>
	Element3 <Scene Town Target !Far 1>
	Element4 <Prefab>
	Element5 <Grab 2>

	% V1 = Initialization / Combat Can Start
	% V2 = Combat Indicator
	% V3 = Email Counter
	% P4 = First time meeting this enemy

	rumor <!5 hired a bounty hunter to kill you.>

	% On a cleanup request just delete this plot.
	cleanup <AdvancePlot 0>

	% At startup, check for victory or failure.
	start <if= P2 3 else GoCheckLoss S= 101 1 S+ 102 1 S= 105 -1 N= 3 E3 ifPersona E4 else cleanup Alert 2 S= 105 1 AdvancePlot 0>
	GoCheckLoss <if= P2 2 else GoRealStart S= 101 1 N= 3 E3 S= 105 -1 AdvancePlot 0 ifPersona E4 Alert 3>

	% The first start, initialize everything.
	GoRealStart <ifPersona E2 else GoDelete if= V1 0 else GoCheckCombat V= 1 1 MoveChar E2 E1 ifPersona N1 else GoInsertE4 DeleteChar E4 E= 4 N1>
	GoInsertE4 <N= 1 E4 P= 4 1>
	GoCheckCombat <if= V1 2 if= SceneID 1 Goto GoStartCombat>
	GoDelete <S= 101 1 N= 3 E3 AdvancePlot 0>

	GoStartCombat <Print 101 Block SavePos P= 1 3 P= 2 1 Dynamic 2 Threat S103 120 .nu1 .nu2 Goto GoPrepNPC>
	GoPrepNPC <PCEnemy E4 NPCLevel E4 S103 DynaNPC E4 2 ComposeD START .start PassVar 1 E4>
	.nu1 <if= T1 0 if= L2 0 L= 2 1 Return>
	.nu2 <if= T2 0 if= L2 0 L= 2 1 XPV 100 Salvage Return>
	.start <if# L1 0 ForceChat L1 L= 1 0>

	% Combat check. If combat has been entered, V2=1.
	% Combat ends in victory if the number of active masters on
	% team two drops to zero, or ends in defeat is the number of
	% active masters on team one drops to zero. After combat, V2=3
	% if the player was victorious or V2=2 is the player lost.
	nu1 <if= T1 0 if= V2 1  V= 2 2  History 4  Reputation 6 -7>
	nu2 <if= T2 0 if= V2 1  V= 2 3  History 5  Reputation 6  1>

	5Min <if= d30 1 if= P1 1 ifG PCCharm d50 if= P3 0 P= 3 1 Email 1>

	Msg1 <\ELEMENT 2 @ \ELEMENT 1 :// I have some information for you. It's delicate... Come see me ASAP.>
	Msg1_1 <\ELEMENT 2 @ \ELEMENT 1 :// Meet me in \ELEMENT 1 for some delicate information. Bring a big gun with you.>
	Msg1_2 <\ELEMENT 2 @ \ELEMENT 1 :// Your life is in danger. I can tell you what I know in \ELEMENT 1 .>

	Msg2 <\ELEMENT 4 : You've got some skills... Killing you will be a challenge! Let's meet again in \ELEMENT 3 .>
	Msg2_1 <\ELEMENT 4 : Most impressive. I must be off to \ELEMENT 3 ...>
	CMsg2_1 <ifG NPCRep E4 3 0 Accept>
	Msg2_2 <\ELEMENT 4 : So the stories are true... You really are a most excellent pilot, but not good enough to survive against \NARRATIVE 2 I'm afraid.>
	CMsg2_2 <ifG 0 NPCRep E4 3 Accept>
	Msg2_3 <\ELEMENT 4 : Hm, it appears that you're a pretty good pilot. We'll meet again in \ELEMENT 3 , only next time I'll be the winner.>
	CMsg2_3 <ifG NPCRep E4 4 0 Accept>
	Msg2_4 <\ELEMENT 4 : You're tougher than I expected... This will be a challenge! Meet me in \ELEMENT 3 and I'll show you what I'm really capable of.>
	CMsg2_4 <ifG 0 NPCRep E4 4 Accept>
	Msg2_5 <\ELEMENT 4 : No fair, you cheated! There's no way you could've beaten me... Meet me again in \ELEMENT 3 and we'll settle things properly!>
	CMsg2_5 <ifG NPCRep E4 5 0 Accept>
	Msg2_6 <\ELEMENT 4 : You have considerable skills... It's too bad you waste them on your doomed fight against \NARRATIVE 2 . Meet me again in \ELEMENT 3 and we can end this.>
	CMsg2_6 <ifG 0 NPCRep E4 5 Accept>

	Msg3 <\ELEMENT 4 : That was too easy. Next time, I'll really destroy you!>
	Msg3_1 <\ELEMENT 4 : Goodbye, \PC .>
	CMsg3_1 <ifG NPCRep E4 3 0 Accept>
	Msg3_2 <\ELEMENT 4 : I'd heard you were a skilled pilot... It appears that I heard wrong.>
	CMsg3_2 <ifG 0 NPCRep E4 3 Accept>
	Msg3_3 <\ELEMENT 4 : Try not to let it upset you, but no-one can defeat me.>
	CMsg3_3 <ifG NPCRep E4 4 0 Accept>
	Msg3_4 <\ELEMENT 4 : Let this be a lesson to you: those who interfere with \NARRATIVE 2 get themselves killed!>
	CMsg3_4 <ifG 0 NPCRep E4 4 Accept>
	Msg3_5 <\ELEMENT 4 : Aw, you got away... This was really fun. I look forward to defeating you again!>
	CMsg3_5 <ifG NPCRep E4 5 0 Accept>
	Msg3_6 <\ELEMENT 4 : Be thankful that you escape with your life. Next time I won't hold back.>
	CMsg3_6 <ifG 0 NPCRep E4 5 Accept>

	Msg4 <You were defeated by the bounty hunter \ELEMENT 4 .>
	Msg4_1 <You were defeated by \ELEMENT 4 outside of \ELEMENT 1 .>
	CMsg4_1 <if= P4 0 Accept>
	Msg5 <You defeated \ELEMENT 4 outside of \ELEMENT 1 .>
	Msg5_1 <You defeated \ELEMENT 4 , a bounty hunter sent by \ELEMENT 5 .>
	CMsg5_1 <if= P4 1 Accept>

	Msg101 <As you leave town, you are attacked by mecha bearing \ELEMENT 5 insignia!>

sub
	Persona 2
	rumor <!2 heard something you might need to know.>
	greeting <NewChat if= P1 1 else GoNoStart Say 1 P= 1 2>
	GoNoStart <SayAnything>
	Msg1 <You should know that \ELEMENT 5 hired a bounty hunter to kill you. Goes by the name of \ELEMENT 4 ; was in town a while back asking questions. Be careful, there may be an ambush waiting when you leave town...>
	Msg1_1 <I heard that you've been fighting against \NARRATIVE 2 . Word on the street is that they've been watching you very carefully. \ELEMENT 4 was hired to kill you... be careful, okay?>
	CMsg1_1 <ifNPCSociable Accept>
	Msg1_2 <You should know \NARRATIVE 2 wants you dead, and they've ordered \ELEMENT 4 to make that happen. Watch your back, there could be an ambush waiting for you at any time.>
	CMsg1_2 <ifNPCShy Accept>
	Msg1_3 <According to the rumors, you might not have long to live... \NARRATIVE 2 has sent \ELEMENT 4 after you. I just thought you'd like to know.>
	CMsg1_3 <ifNPCEasygoing Accept>
	Msg1_4 <I have grave news... \NARRATIVE 2 has decided their battle with you must end, so they've ordered \ELEMENT 4 to kill you. They'll never give up, not until you're dead...>
	CMsg1_4 <ifNPCPassionate Accept>
	Msg1_5 <It's good to see you looking so healthy... people say you're the walking dead. I've learned that \NARRATIVE 2 ordered a hit against you and \ELEMENT 4 plans to collect. Keep your eyes open or you'll walk straight into an ambush.>
	CMsg1_5 <ifNPCCheerful Accept>
	Msg1_6 <You're walking dead... \NARRATIVE 2 ordered \ELEMENT 4 to kill you. Even if \OPR E4 fails it's only a matter of time before someone succeeds... Be careful, there could be an ambush waiting for you anywhere.>
	CMsg1_6 <ifNPCMelancholy Accept>
	Msg1_7 <I'm glad you made it here. There's a contract on your life, funded by \NARRATIVE 2 ... I heard it from my regular source. \ELEMENT 4 has been instructed to not return without your head. Be careful out there, alright?>
	CMsg1_7 <ifG React 25 Accept>

	Persona 4
	greeting <EndChat Say 1>
	Msg1 <You have been a thorn in the side of \NARRATIVE 2 for long enough. So, I have been ordered to remove you, perminently...>
	Msg1_1 <So we meet again. My masters have decided they can no longer tolerate your presence on this world. So, I have been sent to eliminate you.>
	CMsg1_1 <if= P4 0 ifNPCSociable Accept>
	Msg1_2 <It's been decided that you must die.>
	CMsg1_2 <if= P4 0 ifNPCShy Accept>
	Msg1_3 <I hate to interrupt, but \NARRATIVE 2 has decided that you must die. You won't be leaving here alive.>
	CMsg1_3 <if= P4 0 ifNPCEasygoing Accept>
	Msg1_4 <Hello once more, \PC ... My masters have told me that I no longer need to hold back. Today you will face the full extent of my power!>
	CMsg1_4 <if= P4 0 ifNPCPassionate Accept>
	Msg1_5 <Did you miss me? It's time for us to begin the dance once more. My masters in \NARRATIVE 2 have decided that you should die. It will be my pleasure to enact this order.>
	CMsg1_5 <if= P4 0 ifNPCCheerful Accept>
	Msg1_6 <Your worthless life has continued for far too long. Now, it is my task to end it.>
	CMsg1_6 <if= P4 0 ifNPCMelancholy Accept>
	Msg1_7 <Hello \PC . I am \ELEMENT 4 ; \ELEMENT 5 hired me to kill you. Well, that's enough for introductions... now we must fight!!!>
	CMsg1_7 <if= P4 1 Accept>
	Msg1_11 <It's a pleasure to finally meet the famous \PC . You've given \NARRATIVE 2 a lot of trouble, I'm afraid... It's time that you were silenced perminantly.>
	CMsg1_11 <if= P4 1 ifNPCSociable Accept>
	Msg1_12 <I've been sent by \NARRATIVE 2 ... they want you to die, now.>
	CMsg1_12 <if= P4 1 ifNPCShy Accept>
	Msg1_13 <Sorry to interrupt your day, but I've been sent by \NARRATIVE 2 to eliminate you. The name's \ELEMENT 4 .>
	CMsg1_13 <if= P4 1 ifNPCEasygoing Accept>
	Msg1_14 <You have been declared an enemy of \NARRATIVE 2 ... And I've been sent to destroy you!>
	CMsg1_14 <if= P4 1 ifNPCPassionate Accept>
	Msg1_15 <Ah, \PC , it's a pleasure to finally meet. My name is \ELEMENT 4 and I will be killing you for \NARRATIVE 2 today.>
	CMsg1_15 <if= P4 1 ifNPCCheerful Accept>
	Msg1_16 <Your death approaches, \PC ... I have been hired by \NARRATIVE 2 to end your worthless life.>
	CMsg1_16 <if= P4 1 ifNPCMelancholy Accept>

end
inv
	NPC Bounty Hunter
	EquipChar 45000
	CharDesc Villainous

end

