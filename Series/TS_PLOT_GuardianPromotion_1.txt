Plot 0
	name <Promotion Reward>
	% The PC has gone up a rank in the military, and will
	% now get a reward for it.

	% Element 1 is the calling faction
	% Element 2 is the character who'll give the PC the reward
	Element2 <Character Guardian>

	% These are faction rewards...
	Element3 <Prefab>
	Element4 <Prefab>
	Element5 <Prefab>
	Element6 <Prefab>

	% V1 = Email Indicator

	% This plot will remain active until the PC collects his
	% reward, until the NPC dies, or until the PC is no longer
	% a member of the calling faction.
	start <ifNPCOK E2 else GoReschedule if= PCFac E1 else GoDelete if= V1 0 Email d5 V= 1 1>
	GoReschedule <FacVar+ E1 1 1 AdvancePlot 0>
	GoDelete <AdvancePlot 0>

	Msg1 <\ELEMENT 2 @ \SCENE EScene 2 :// You've earned a promotion. Come see me to accept your reward.>
	Msg2 <\ELEMENT 2 @ \SCENE EScene 2 :// I have urgent business which requires your attention at once.>
	Msg3 <\ELEMENT 2 @ \SCENE EScene 2 :// Report here for debriefing at once, \RANK .>
	Msg4 <\ELEMENT 2 @ \SCENE EScene 2 :// You have been promoted. Report here to receive your commission.>
	Msg5 <\ELEMENT 2 @ \SCENE EScene 2 :// Your presence is required at headquarters immediately.>

sub
	Persona 2
	% V1 = Faction level the PC is being rewarded for.
	greeting <NewChat XPV 100 V= 1 FacVar E1 2 V+ 1 1 FacVar= E1 2 V1 AdvancePlot 0 Goto GoL0>
	GoL0 <ifG 9 V1 else GoBigLevel ifG FXPNeeded V1 PCRep 6 else GoL1 CashPrize Reward Threat FXPNeeded V1 100 250 Say 10>
	GoL1 <if= V1 1 else GoL2 Say 1 MechaPrize .mek1>
	.mek1 <Corsair.txt>
	GoL2 <if= V1 2 else GoL3 Say 2 CashPrize 250000>
	GoL3 <if= V1 3 else GoL4 Say 3 GiveKeyItem E3>
	GoL4 <if= V1 4 else GoL5 Say 4 MechaPrize .mek2>
	.mek2 <Vadel.txt>
	GoL5 <if= V1 5 else GoL6 Say 5 GiveKeyItem E4 GiveKeyItem E6>
	GoL6 <if= V1 7 else GoL7 Say 6 PCSkill+ 5 2 PCSkill+ 10 2>
	GoL7 <if= V1 6 else GoL8 Say 7 GiveKeyItem E5>
	GoL8 <if= V1 8 else GoBigLevel Say 8 MechaPrize .mek3>
	.mek3 <Monstrous.txt>
	GoBigLevel <Say 9 CashPrize 500000>

	Msg1 <As you have been promoted to the rank of \FacRank E1 V1 , you will need a spare mecha. This battroid will be more versatile than the patrol car you were originally given. In any case, it's always good to have a spare vehicle.>
	Msg2 <You have been promoted to the rank of \FacRank E1 V1 . You could probably use some new equipment. Here's a cash bonus.>
	Msg3 <You have been promoted to the rank of \FacRank E1 V1 . You've earned the right to carry this officer's sidearm, which should keep the bad guys at bay.>
	Msg4 <You have been promoted to the rank of \FacRank E1 V1 . From here on in you'll be tackling our most dangerous missions, and you'll need to stay one step ahead of your foes. Here's a new mecha that should serve the purpose.>
	Msg5 <You have been promoted to the rank of \FacRank E1 V1 . You may be called upon to engage criminals face to face, and for that you're going to need some protection. This S.W.A.T armor is the most advanced in our arsenal.>
	Msg6 <You have been promoted to the rank of \FacRank E1 V1 . In order to help you perform your job well, you've been enrolled in a special defensive strategy training course.>
	Msg7 <You have been promoted to the rank of \FacRank E1 V1 . As a symbol of your new rank, take this weapon. Only the most decorated of guardians are entitled to use it.>
	Msg8 <You have been promoted to the rank of \FacRank E1 V1 . Here's a heavy patrol car for your use. Really heavy. Actually, it's not so much a car as it is a tank... Try not to damage the city streets when you park it.>
	Msg9 <Here is a cash reward in recognition of your dedicated service to \ELEMENT 1 .>
	Msg10 <You have been promoted to the rank of \FacRank E1 V1 . Here is a small cash reward in recognition of your service.>

end
inv
	Gun 6
	name <Peacemaker>
	Range 5
	BV 3
	Magazine 80
	mass -7
	Acc 1
	Recharge 7
	sub
		ammo 6
		type <ARMORPIERCING>
		mass -1
	end

	BodyArmor 9
	name <SWAT Armor>
	Mass -13
	sub
		MLauncher 3
		name <Rocket Pack>
		Acc -1
		Magazine 30
		range 8
		type <SWARM>
		sub
			Rockets 3
		end
	end

	Melee 23
	name <Hammer of Justice>
	mass -15
	type <BRUTAL EXTEND THROWN RETURNING>
	Acc 2
	Recharge 4

	HeadArmor 9
	name <SWAT Helmet>
	mass -16
	sub
		PCS 3
		mass -2
	end


end
